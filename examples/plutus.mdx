---
title: Plutus Examples
---

## Vesting Example

```tx3
party Owner;
party Beneficiary;

policy TimeLock = 0x6b9c456aa650cb808a9ab54326e039d5235ed69f069c9664a8fe5b69;

type State {
    lock_until: Int,
    owner: Bytes,
    beneficiary: Bytes,
}

tx lock(
    quantity: Int,
    until: Int
) {
    input source {
        from: Owner,
        min_amount: Ada(quantity) + fees,
    }

    output target {
        to: TimeLock,
        amount: Ada(quantity),
        datum: State {
            lock_until: until,
            owner: Owner,
            beneficiary: Beneficiary,
        },
    }

    output {
        to: Owner,
        amount: source - Ada(quantity) - fees,
    }
}

tx unlock(
    locked_utxo: UtxoRef
) {
    input gas {
        from: Beneficiary,
        min_amount: fees,
    }

    input locked {
        from: TimeLock,
        ref: locked_utxo,
        redeemer: (),
    }

    output target {
        to: Beneficiary,
        amount: gas + locked - fees,
    }
}
```

## Order-book Example

```tx3
party Sender;

policy OrderBook {
    hash: 0x7039c520d0627aafa728f7e4dd10142b77c257813c36f57e2cb88f72a5,
    script: 0xfacd82a32c6692dd60b6b930a86569d4f7a5558684bebc7b2f34259f0dc20b07#0,
}

asset ControlToken = 0x39c520d0627aafa728f7e4dd10142b77c257813c36f57e2cb88f72a5."CONTROL";
asset Bid = 0x39c520d0627aafa728f7e4dd10142b77c257813c36f57e2cb88f72a5."BID";

type AssetClass {
    policy: Bytes,
    name: Bytes,
}

type DatumValue {
    asset_class: AssetClass,
    amount: Int,
}

type OrderInfo {
    sender_address: Bytes,
    token: DatumValue,
}


type OrderDatum {
    info: OrderInfo,
    control_asset_class: AssetClass,
}


tx new_order() {
    reference contract {
        ref: 0xfacd82a32c6692dd60b6b930a86569d4f7a5558684bebc7b2f34259f0dc20b07#0,
    }

    input source {
        from: Sender,
        min_amount: fees + Bid(1) + Ada(2000000),
    }

    collateral {
        ref: 0x5f820f170b7a7322f51e51fe6ed871d35b312d5ebcd6ac37cd2f9cfdf50f8c85#9,
    }

    mint {
        amount: ControlToken(1),
        redeemer: (),
    }

    output change {
        to: Sender,
        amount: source - Ada(2000000) - fees - Bid(1),
    }

    output order {
        to: OrderBook,
        amount: Ada(2000000) + Bid(1) + ControlToken(1),
        datum: OrderDatum {
            info: OrderInfo {
                sender_address: Sender,
                token: DatumValue {
                    asset_class: AssetClass {
                        policy: 0x39c520d0627aafa728f7e4dd10142b77c257813c36f57e2cb88f72a5,
                        name: "ASK",
                    },
                    amount: 1,
                },
            },
            control_asset_class: AssetClass {
                policy: 0x39c520d0627aafa728f7e4dd10142b77c257813c36f57e2cb88f72a5,
                name: "CONTROL",
            },
        },
    }
    metadata {
        674: "SOME METADATA",
    }
}
```
